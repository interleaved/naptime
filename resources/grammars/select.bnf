<S> = token (<','> token)*
<token> = (table / column )

<col> = '"' (table-name <'.'>)* (wildcard / json-column / typed-column / renamed-column / column-with-spaces) '"' / (table-name <'.'>)* (wildcard / json-column / typed-column / renamed-column / column-with-spaces)

<table> = (table-columns / table-hint / table-alias)

table-hint = table-name <#'!(?!inner)'> (table-columns / table-alias)
table-alias = table-name <':'> table-columns
table-columns = table-name bang-inner? <'('> S <')'>

<column> = (wildcard / json-column / typed-column / renamed-column / regular-column)

wildcard = <'*'>
json-column = (regular-column (json-key / json-nth))+ (typed-column / renamed-column / regular-column)
typed-column = regular-column <'::'> type
renamed-column = regular-column <':'> regular-column
regular-column = #'[a-zA-Z0-9_]+'
column-with-spaces = #'[a-zA-Z0-9_\ ]+'

<type> = (complex-type / regular-type)
regular-type = #'[a-zA-Z0-9_ ]+'
complex-type = regular-type <'('> number (<','> number)* <')'>
number = #'[0-9]+'

json-key = <'->>'>
json-nth = <'->'>

table-name = #'[a-zA-Z0-9_]+'
bang-inner = <'!inner'>