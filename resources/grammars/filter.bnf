S = condition (<','> condition)*
condition = not? table-name? column-name? op
not = <'not.'>
op = (eq|gt|gte|lt|lte|neq|like|ilike|in|is|fts|plfts|phfts|wfts|or|and)
eq = <dot? 'eq' dot> value
gt = <dot? 'gt' dot> value
gte = <dot? 'gte' dot> value
lt = <dot? 'lt' dot> value
lte = <dot? 'lte' dot> value
neq = <dot? 'neq' dot> value
like = <dot? 'like' dot> value
ilike = <dot 'ilike' dot> value
in = <dot? 'in' dot? '('> value (<','> value)* <')'>
is = <dot? 'is.'>('null'|'true'|'false'|'unknown')
fts = <dot? 'fts'> locale <dot> text-value / <'fts.'> text-value
plfts = <dot? 'plfts' dot> text-value
phfts = <dot? 'phfts' dot> text-value
wfts = <dot? 'wfts' dot> text-value
or = <dot? 'or'> or-body
or-body = <'('> S <')'>
and = <'and('> S <')'>
<dot> = '.'
locale = <'('> #'[a-zA-Z0-9_]+' <')'>
<value> = (numeric-value|text-value)
<numeric-value> = (number)
<text-value> = (bare-string|quoted-string)
number = #'[0-9]+'
bare-string = #'[^,)]+'
quoted-string = <'\"'> #'[^\"]+' <'\"'>
table-name = #'[a-zA-Z0-9_]+'
column-name = dot? #'[a-zA-Z0-9_]+'